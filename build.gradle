plugins {
    id 'java'
    id("io.papermc.paperweight.userdev") version "2.0.0-beta.14"
    id("com.github.johnrengelman.shadow") version "8.1.1"
}

group = 'net.ixbob.thepit'
version = '0.0.1'

repositories {
    mavenCentral()
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven { url = uri("https://repo.codemc.io/repository/maven-releases/") }
    maven { url = uri("https://repo.codemc.io/repository/maven-snapshots/") }
}

dependencies {
    paperweight.paperDevBundle("1.21.4-R0.1-SNAPSHOT")
    implementation "com.github.retrooper:packetevents-spigot:2.7.0"
    implementation 'org.mongodb:mongodb-driver-sync:5.2.1'
    testImplementation platform('org.junit:junit-bom:5.11.4')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

tasks.assemble {
    // make the reobfJar task run on the default build task
    dependsOn(tasks.reobfJar)
}

tasks.register('moveOutputToTestServer') {
    doLast {
        def testServerDir = project.hasProperty('testServerDir') ? project.testServerDir : "${projectDir}/test-server"

        def pluginsDir = file("${testServerDir}/plugins")
        if (!pluginsDir.exists()) {
            pluginsDir.mkdirs()
        }

        def jarFiles = fileTree("${projectDir}/build/libs").matching { include "*-reobf.jar" }
        jarFiles.each {jar ->
            copy {
                from jar
                into pluginsDir
            }
            println "Moved ${jar.name} to ${pluginsDir}"
        }
    }
}
